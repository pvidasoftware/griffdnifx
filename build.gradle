buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath 'org.codehaus.griffon:gradle-griffon-plugin:2.7.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
        classpath 'org.gradle.api.plugins:gradle-izpack-plugin:0.2.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'com.github.cr0:gradle-macappbundle-plugin:3.1.0'
        classpath 'org.kordamp.gradle:stats-gradle-plugin:0.2.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
        classpath 'de.gliderpilot.gradle.jnlp:gradle-jnlp-plugin:0.2.1'
        classpath 'net.nemerosa:versioning:2.1.0'
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.3"
}

apply plugin: 'groovy'
apply plugin: 'org.codehaus.griffon.griffon'
apply plugin: 'net.nemerosa.versioning'

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    macosx = System.getProperty('os.name').contains('Mac OS')
}

griffon {
    disableDependencyResolution = false
    includeGroovyDependencies = true
    version = '2.7.0'
    toolkit = 'javafx'
    applicationProperties = [
        'build.date'    : buildDate,
        'build.time'    : buildTime,
        'build.revision': versioning.info.commit
    ]
}

mainClassName = 'com.puravida.Launcher'

apply from: 'gradle/publishing.gradle'
apply from: 'gradle/code-coverage.gradle'
apply from: 'gradle/code-quality.gradle'
apply from: 'gradle/javafx-plugin.gradle'
apply from: 'gradle/integration-test.gradle'
apply from: 'gradle/functional-test.gradle'
apply from: 'gradle/package.gradle'
apply from: 'gradle/docs.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.kordamp.gradle.stats'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.kt3k.coveralls'

javafx {
    mainClass = mainClassName
}

dependencies {
    compile "org.codehaus.griffon:griffon-guice:${griffon.version}"

    runtime 'org.slf4j:slf4j-simple:1.7.21'

    testCompile "org.codehaus.griffon:griffon-javafx-test:${griffon.version}"
    testCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    testCompile "org.spockframework:spock-core:${spockVersion}"

    functionalTestCompile "org.codehaus.griffon:griffon-javafx-test:${griffon.version}"

    //add plugins
    griffon 'org.codehaus.griffon.plugins:griffon-wslite-plugin:1.1.0'
    griffon 'org.codehaus.griffon.plugins:griffon-glazedlists-plugin:1.3.1'
    griffon 'org.codehaus.griffon.plugins:griffon-fontawesome-plugin:1.4.0'

    compile 'org.kordamp.bootstrapfx:bootstrapfx-core:0.2.0'

    compile 'es.gob:jmulticard-jse:1.4'
    compile 'es.gob:jmulticard-ui:1.4'
    compile 'org.bouncycastle:bcprov-jdk15on:1.54'
    compile 'org.bouncycastle:bcmail-jdk15on:1.54'
    compile 'org.apache.pdfbox:pdfbox:2.0.1'

}

tasks.withType(JavaCompile) {
    sourceCompatibility = project.sourceCompatibility
    targetCompatibility = project.targetCompatibility
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = project.sourceCompatibility
    targetCompatibility = project.targetCompatibility
}

import com.github.jengelman.gradle.plugins.shadow.transformers.*
import java.text.SimpleDateFormat

shadowJar {
    transform(ServiceFileTransformer)
    transform(ServiceFileTransformer) {
        path = 'META-INF/griffon'
    }
    transform(ServiceFileTransformer) {
        path = 'META-INF/types'
    }
    transform(PropertiesFileTransformer) {
        paths = [
            'META-INF/editors/java.beans.PropertyEditor'
        ]
    }
}

startScripts {
    doLast {
        if (!macosx) unixScript.text = unixScript.text.replaceAll('"(-Xdock:(name|icon)=)([^"]*?)(")', ' ')
        windowsScript.text = windowsScript.text.replaceAll('"(-Xdock:(name|icon)=)([^"]*?)(")', ' ')
    }
}

if (hasProperty('debugRun') && ((project.debugRun as boolean))) {
    run {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
    }
}

task jacocoRootMerge(type: org.gradle.testing.jacoco.tasks.JacocoMerge, dependsOn: [test, jacocoTestReport, jacocoIntegrationTestReport, jacocoFunctionalTestReport]) {
    executionData = files(jacocoTestReport.executionData, jacocoIntegrationTestReport.executionData, jacocoFunctionalTestReport.executionData)
    destinationFile = file("${buildDir}/jacoco/root.exec")
}

task jacocoRootReport(dependsOn: jacocoRootMerge, type: JacocoReport) {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports after running all tests.'
    executionData file("${buildDir}/jacoco/root.exec")
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output)
    reports {
        csv.enabled = false
        xml.enabled = true
        html.enabled = true
        html.destination = "${buildDir}/reports/jacoco/root/html"
        xml.destination = "${buildDir}/reports/jacoco/root/root.xml"
    }
}

tasks.withType(JavaExec) {
    // Assign all Java system properties from
    // the command line to the JavaExec task.
    systemProperties System.properties
}

apply plugin: 'de.gliderpilot.jnlp'
jnlp {
    useVersions  false
    usePack200  false
    codebase "https://pvidasoftware.github.io/griffdnifx/"
    spec '7.0'
    j2seParams = [version: '7.0+', 'max-heap-size': '256m']
    signJarAddedManifestEntries = [
            'Codebase': 'https://pvidasoftware.github.io/griffdnifx/',
            'Permissions': 'all-permissions',
            'Application-Name': "${project.name}"
    ]
    signJarRemovedManifestEntries = '(?:Trusted-Only|Trusted-Library|.*-Digest)'
    withXml {
        information {
            title project.name
            vendor project.group ?: project.name
        }
        security {
            'all-permissions'()
        }
    }
    desc = {
        'application-desc'('main-class': "${project.jnlp.mainClassName}") {
            argument('-verbose')
        }
    }
    signJarParams = [alias: keyAlias, storepass: keystorePwd, keystore:'keystore']
}

asciidoctor {
    attributes 'source-highlighter': 'prettify', 'toc': 'left', 'setanchors': 'true'
    dependsOn 'build'

    resources {
        from('src/docs/screenshot') {
            include '**'
        }
        into './images'
    }
}

task genkey << {
    ant.genkey(alias: keyAlias , storepass: keystorePwd, keystore:'keystore',
            dname: 'CN=Ant Group, OU=Jakarta Division, O=Apache.org, C=US') }

task preparePublish(type: Copy){
    dependsOn 'asciidoctor'
    delete 'build/publish'
    mkdir 'build/publish'
    from 'build/asciidoc/html5'
    from 'build/jnlp'
    into 'build/publish'
}


task publish(type: GradleBuild) {
    dependsOn 'preparePublish'
    buildFile = 'publish.gradle'
    tasks = ['publishGhPages']
}
